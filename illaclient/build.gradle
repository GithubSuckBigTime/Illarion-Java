/*
 * This file is part of the Illarion Client.
 *
 * Copyright Â© 2013 - Illarion e.V.
 *
 * The Illarion Client is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Illarion Client is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Illarion Client.  If not, see <http://www.gnu.org/licenses/>.
 */
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

archivesBaseName = 'illarion_client'
mainClassName = 'illarion.client.IllaClient'

dependencies {
    gitVersion

    compile project(':common')
    compile project(':engine')
    runtime project(':engine-libgdx')
    compile project(':engine-nifty')
    compile project(':nifty-controls')
    runtime project(':nifty-style')
    compile group: 'javolution', name: 'javolution', version: '5.5.1'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.1'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    compile group: 'org.bushe', name: 'eventbus', version: '1.4'
    compile group: 'lessvoid', name: 'nifty', version: '1.4.0-SNAPSHOT'
    runtime group: 'org.illarion', name: 'rsc_books', version: project.version
    runtime group: 'org.illarion', name: 'rsc_chars', version: project.version
    runtime group: 'org.illarion', name: 'rsc_effects', version: project.version
    runtime group: 'org.illarion', name: 'rsc_gui', version: project.version
    runtime group: 'org.illarion', name: 'rsc_items', version: project.version
    runtime group: 'org.illarion', name: 'rsc_music', version: project.version
    runtime group: 'org.illarion', name: 'rsc_sounds', version: project.version
    runtime group: 'org.illarion', name: 'rsc_tables', version: project.version
    runtime group: 'org.illarion', name: 'rsc_tiles', version: project.version
}

jar {
    doFirst {
        gitVersion

        manifest {
            attributes    \
               'Implementation-Title': 'Illarion Client',    \
               'Implementation-Version': project.ext.fullVersion,    \
               'Implementation-Vendor': 'Illarion e.V.',    \
               'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ssZ'),    \
               'Built-By': System.getProperty('user.name'),    \
               'Built-JDK': System.getProperty('java.version'),    \
               'Main-Class': 'illarion.client.IllaClient'
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "file://$buildDir/repo"
        }
    }
}