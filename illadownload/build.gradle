/*
 * This file is part of the Illarion Client.
 *
 * Copyright Â© 2013 - Illarion e.V.
 *
 * The Illarion Client is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Illarion Client is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Illarion Client.  If not, see <http://www.gnu.org/licenses/>.
 */
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply from: '../javafx.plugin'

archivesBaseName = 'illarion_download'
mainClassName = 'illarion.download.gui.GuiApplication'

dependencies {
    compile project(':common')
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.2'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'org.eclipse.aether', name: 'aether-impl', version: '0.9.0.M3'
    compile group: 'org.eclipse.aether', name: 'aether-transport-http', version: '0.9.0.M3'
    compile group: 'org.eclipse.aether', name: 'aether-connector-basic', version: '0.9.0.M3'
    compile group: 'org.apache.maven', name: 'maven-aether-provider', version: '3.1.1'
}

jar {
    doFirst {
        gitVersion

        manifest {

            attributes     \
                'Main-Class': 'illarion.download.gui.GuiApplication',    \
                'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ssZ'),    \
                'Built-By': System.getProperty('user.name'),     \
                'Built-JDK': System.getProperty('java.version'),     \
                'Implementation-Title': 'Illarion Launcher',     \
                'Implementation-Vendor': 'Illarion e.V.',    \
                'Implementation-Version': project.ext.fullVersion
        }
    }
}

publish {
    doFirst {
        jfxDeploy
    }
}

javafx {
    appID 'IllaLauncher'
    appName 'Illarion Launcher'
    mainClass project.mainClassName

    embedLauncher = false

    category = 'Games'
    copyright = 'Copyright (c) 2013 Illarion e.V.'
    description = 'This is the downloader and launcher of Illarion.'
    licenseType = 'AGPLv3'
    vendor = 'Illarion e.V.'
    installSystemWide = true
    menu = true
    shortcut = true

    if (project.hasProperty('keyFile')) {
        apply from: project.keyFile
        releaseKey {
            alias = project.ext.storealias
            keyStore = project.ext.keystore
            storePass = project.ext.storepass
            storeType = project.ext.storetype
        }
        signingMode 'release'
    }
    packaging 'none'

    profiles {
        windows {
            javaRuntime = '<NO RUNTIME>'
        }
        macosx {
            javaRuntime = '<NO RUNTIME>'
        }
        linux {
            javaRuntime = '<NO RUNTIME>'
        }
    }

    width = 620
    height = 410
    embedJNLP = false
    codebase = 'http://illarion.org/media/java/launcher'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task copyDistribution(type: Copy) {
    from project.distsDir
    into project.targetDistDir
}

jfxDeploy {
    doLast {
        if (project.hasProperty('targetDistDir')) {
            copyDistribution
        }
    }
}

if (project.hasProperty('targetDistDir')) {
    project.distsDir = project.targetDistDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url project.ext.mavenRepo
        }
    }
}