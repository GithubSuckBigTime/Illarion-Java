/*
 * This file is part of the Illarion project.
 *
 * Copyright Â© 2014 - Illarion e.V.
 *
 * Illarion is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Illarion is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

apply plugin: 'idea'

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

idea {
    project {
        languageLevel = '1.7'
    }
}

allprojects { project ->
    if (project.hasProperty('targetRepo')) {
        project.ext.mavenRepo = "file://$targetRepo"
    } else {
        project.ext.mavenRepo = "file://${project.buildDir}/repo"
    }

    task gitVersion { task ->
        def gitVersion = 'unknown'
        def branch = 'develop'
        def File rootDir = project.rootDir
        for (def gitCmd in ['git', 'git.cmd']) {
            try {
                def procBranch = "$gitCmd rev-parse --abbrev-ref HEAD".execute(null as List, rootDir)
                procBranch.waitFor()
                branch = procBranch.in.text.trim()
                procBranch.destroy()

                def procVersion = "$gitCmd describe --tags HEAD".execute(null as List, rootDir)
                procVersion.waitFor()
                gitVersion = procVersion.in.text.trim()
                procVersion.destroy()

                break
            } catch (e) {
                logger.error('Failed to get version from Git', e)
            }
        }

        def String mainVersion
        def indexOfSeparator = gitVersion.indexOf('-')
        if (indexOfSeparator == -1) {
            mainVersion = gitVersion
        } else {
            mainVersion = gitVersion.substring(0, indexOfSeparator)
        }

        if (branch == 'master') {
            if (indexOfSeparator > -1) {
                throw new RuntimeException("Can't build snapshot in master branch.")
            }
        } else {
            mainVersion += '-SNAPSHOT'
        }

        task.project.version = mainVersion
        task.project.ext.fullVersion = gitVersion
    }

    project.configurations.all { config ->
        resolutionStrategy {
            cacheDynamicVersionsFor 10 * 60, 'seconds'
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.sf.proguard', name: 'proguard-gradle', version: '4.11'
        classpath group: 'edu.sc.seis.gradle', name: 'launch4j', version: '1.0.6'
        classpath group: 'edu.sc.seis.gradle', name: 'macappbundle', version: '2.0.0'
    }
}

subprojects { project ->
    project.group = 'org.illarion'

    project.plugins.withType(JavaPlugin) {
        project.sourceCompatibility = JavaVersion.VERSION_1_7
        project.targetCompatibility = JavaVersion.VERSION_1_7
        project.tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }
        project.test {
            useTestNG()
        }
        project.apply { 'idea' }
        project.configurations {
            provided
        }
        project.sourceSets {
            main.compileClasspath += configurations.provided
            test.compileClasspath += configurations.provided
            test.runtimeClasspath += configurations.provided
        }
        project.dependencies {
            compile group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.3'
        }
        idea {
            module {
                if (scopes.PROVIDED != null) {
                    scopes.PROVIDED.plus += configurations.provided
                }
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'http://illarion.org/media/java/maven'
        }
        maven {
            url 'http://oss.sonatype.org/content/repositories/releases/'
        }
    }
}